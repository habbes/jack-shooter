/**
 * class representing a bullet shot from a tank's gun
 *
 * Bullet has common methods with other game objects used
 * in the rendering cycle
 */
class Bullet {
    field SceneObject so;

    constructor Bullet new (int d) {
        var int w, h;
        if (d < 3) {
            // vertical
            let w = 2;
            let h = 6;
        }
        else {
            // horizontal
            let w = 6;
            let h = 2;
        }
        let so = SceneObject.new(0, 0, w, h, d, 4);
        return this;
    }

    method SceneObject getSceneObject () {
        return so;
    }

    method void move () {
        var int d;
        let d = so.getDirection();
        if (d = 1) { do so.setY(so.getY() - so.getStep()); }
        if (d = 2) { do so.setY(so.getY() + so.getStep()); }
        if (d = 3) { do so.setX(so.getX() - so.getStep()); }
        if (d = 4) { do so.setX(so.getX() + so.getStep()); }
        return;
    }

    method void draw () {
        do Viewport.fillArea(so.getX(), so.getY(), so.getWidth(), so.getHeight());
        return;
    }
    
    method void onRefresh (SceneManager scene) {
        do move();
        return;
    }

    method void dispose () {
        do so.dispose();
        do Memory.deAlloc(this);
        return;
    }
}