class Tank {
    field int x, y, lastX, lastY, width, height;
    field int direction, step;
    field boolean dirty;

    constructor Tank new () {
        let width = 16;
        let height = 16;
        let x = 0;
        let y = 0;
        let direction = 1;
        let step = 2;
        let dirty = false;
        return this;
    }

    method int getWidth () {
        return width;
    }

    method int getHeight () {
        return height;
    }

    method int getX () {
        return x;
    }

    method int getY () {
        return y;
    }

    method int getLastX () {
        return lastX;
    }

    method int getLastY() {
        return lastY;
    }

    method void setCoord (int Ax, int Ay) {
        let lastX = x;
        let lastY = y;
        let x = Ax;
        let y = Ay;
        let dirty = true;
        return;
    }

    method void setX(int Ax) {
        do setCoord(Ax, y);
        return;
    }

    method void setY(int Ay) {
        do setCoord(x, Ay);
        return;
    }

    method boolean shouldUpdate () {
        return dirty;
    }

    method void markClean () {
        let dirty = false;
        return;
    }

    method void move (int d) {
        if (direction = d) {
            do moveForward();
        }
        else {
            do turn(d);
        }
        return;
    }

    method void moveForward () {
        if (direction = 1) { 
            do setY(getY() - step);
        }
        if (direction = 2) {
            do setY(getY() + step);
        }
        if (direction = 3) {
            do setX(getX() - step); 
        }
        if (direction = 4) {
            do setX(getX() + step);
        }
        return;
    }

    method void turn (int d) {
        let direction = d;
        return;
    }

    method void turnUp () {
        do turn(1);
        return;
    }

    method void turnDown () {
        do turn(2);
        return;
    }

    method void turnLeft () {
        do turn(3);
        return;
    }

    method void turnRight () {
        do turn(4);
        return;
    }

    method Bullet shoot () {
        var Bullet b;
        var int bx, by;
        let b = Bullet.new(direction);
        if (direction = 1) {
            do b.setCoord(x + 7, (y - 1) - b.getHeight());
        }
        if (direction = 2) {
            do b.setCoord(x + 7, y + height);
        }
        if (direction = 3) {
            do b.setCoord((x - 1) - b.getWidth(), y + 7);
        }
        if (direction = 4) {
            do b.setCoord(x + width, y + 7);
        }
        return b;
    }

    method void draw() {
        if (direction = 1) { do drawUp(); }
        if (direction = 2) { do drawDown();}
        if (direction = 3) { do drawLeft();}
        if (direction = 4) { do drawRight();}
        return;
    }

    method void drawUp () {
        // left track
        do Viewport.fillArea(x, y + 1, 4, 15);
        // left part of body
        do Viewport.fillArea(x + 4, y + 3, 3, 11);
        // gun
        do Viewport.fillArea(x + 7, y, 2, 11);
        // right part of body
        do Viewport.fillArea(x + 9, y + 3, 3, 11);
        // right track
        do Viewport.fillArea(x + 12, y + 1, 4, 15);
        return;
    }

    method void drawDown () {
        do Viewport.fillArea(x, y, 4, 15);
        do Viewport.fillArea(x + 4, y + 2, 3, 11);
        do Viewport.fillArea(x + 7, y + 5, 2, 11);
        do Viewport.fillArea(x + 9, y + 2, 3, 11);
        do Viewport.fillArea(x + 12, y, 4, 15);
        return;
    }

    method void drawLeft () {
        do Viewport.fillArea(x + 1, y, 15, 4);
        do Viewport.fillArea(x + 3, y + 4, 11, 3);
        do Viewport.fillArea(x, y + 7, 11, 2);
        do Viewport.fillArea(x + 3, y + 9, 11, 3);
        do Viewport.fillArea(x + 1, y + 12, 15, 4);
        return;
    }

    method void drawRight () {
        do Viewport.fillArea(x, y, 15, 4);
        do Viewport.fillArea(x + 2, y + 4, 11, 3);
        do Viewport.fillArea(x + 5, y + 7, 11, 2);
        do Viewport.fillArea(x + 2, y + 9, 11, 3);
        do Viewport.fillArea(x, y + 12, 15, 4);
        return;
    }

    method void clear() {
        do Viewport.clearArea(x, y, width, height);
        return;
    }

    method void dispose () {
        do Memory.deAlloc(this);
        return;
    }
}