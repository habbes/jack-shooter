class Tank {
    field int width, height;
    field Coord coord, lastCoord;
    field int direction, step;
    field boolean dirty;

    constructor Tank new () {
        let width = 16;
        let height = 16;
        let coord = Coord.origin();
        let direction = 1;
        let step = 2;
        let dirty = false;
        return this;
    }

    method int getWidth () {
        return width;
    }

    method int getHeight () {
        return height;
    }

    method Coord getCoord () {
        return coord;
    }

    method Coord getLastCoord () {
        return lastCoord;
    }

    method void setCoord (Coord c) {
        let lastCoord = coord;
        let coord = c;
        let dirty = true;
        return;
    }

    method boolean shouldUpdate () {
        return dirty;
    }

    method void move (int d) {
        if (direction = d) {
            do moveForward();
        }
        else {
            do turn(d);
        }
        return;
    }

    method void moveForward () {
        if (direction = 1) { 
            do setCoord(coord.moveY(-step));
        }
        if (direction = 2) {
            do setCoord(coord.moveY(step));
        }
        if (direction = 3) {
            do setCoord(coord.moveX(-step)); 
        }
        if (direction = 4) {
            do setCoord(coord.moveX(step));
        }
        return;
    }

    method void turn (int d) {
        let direction = d;
        return;
    }

    method void turnUp () {
        do turn(1);
        return;
    }

    method void turnDown () {
        do turn(2);
        return;
    }

    method void turnLeft () {
        do turn(3);
        return;
    }

    method void turnRight () {
        do turn(4);
        return;
    }

    method void draw() {
        if (direction = 1) { do drawUp(); }
        if (direction = 2) { do drawDown();}
        if (direction = 3) { do drawLeft();}
        if (direction = 4) { do drawRight();}
        let dirty = false;
        return;
    }

    method void drawUp () {
        // left track
        do Viewport.fillArea(coord.moveY(1), 4, 15);
        // left part of body
        do Viewport.fillArea(coord.move(4, 3), 3, 11);
        // gun
        do Viewport.fillArea(coord.moveX(7), 2, 11);
        // right part of body
        do Viewport.fillArea(coord.move(9, 3), 3, 11);
        // right track
        do Viewport.fillArea(coord.move(12, 1), 4, 15);
        return;
    }

    method void drawDown () {
        do Viewport.fillArea(coord, 4, 15);
        do Viewport.fillArea(coord.move(4, 2), 3, 11);
        do Viewport.fillArea(coord.move(7, 5), 2, 11);
        do Viewport.fillArea(coord.move(9, 2), 3, 11);
        do Viewport.fillArea(coord.moveX(12), 4, 15);
        return;
    }

    method void drawLeft () {
        do Viewport.fillArea(coord.moveX(1), 15, 4);
        do Viewport.fillArea(coord.move(3, 4), 11, 3);
        do Viewport.fillArea(coord.moveY(7), 11, 2);
        do Viewport.fillArea(coord.move(3, 9), 11, 3);
        do Viewport.fillArea(coord.move(1, 12), 15, 4);
        return;
    }

    method void drawRight () {
        do Viewport.fillArea(coord, 15, 4);
        do Viewport.fillArea(coord.move(2, 4), 11, 3);
        do Viewport.fillArea(coord.move(5, 7), 11, 2);
        do Viewport.fillArea(coord.move(2, 9), 11, 3);
        do Viewport.fillArea(coord.moveY(12), 15, 4);
        return;
    }

    method void clear() {
        do Viewport.clearArea(coord, width, height);
        return;
    }

    method void dispose () {
        do cood.dispose();
        do Memory.deAlloc(this);
        return;
    }
}